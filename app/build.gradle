buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/populov/maven' }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 24
        applicationId 'com.fwest98.fingify'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        if(System.getenv("TRAVIS")) {
            release {
                storeFile rootProject.file('releasekeystore.jks')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        } else {
            release {
                storeFile rootProject.file('releasekeystore.jks')
                storePassword ""
                keyAlias ""
                keyPassword ""
            }
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "GRADLE_DEBUG", "true"
            applicationIdSuffix ".debug"
        }

        prerelease {
            buildConfigField "boolean", "GRADLE_DEBUG", "true"
        }

        release {
            buildConfigField "boolean", "GRADLE_DEBUG", "false"
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        maxProcessCount = 4
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Google libs
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.android.gms:play-services-base:8.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:design:24.0.0'

    // Barcodescanner libs
    compile project(':BarcodeScanner')
    compile 'com.google.zxing:core:3.2.0'

    // Functional libs
    provided 'org.projectlombok:lombok:1.16.6'
    compile 'javax.annotation:jsr250-api:1.0'

    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'

    compile 'org.functionaljava:functionaljava:4.4'

    compile 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
    compile 'ch.acra:acra:4.9.0'

    // Design libs
    compile 'com.mikepenz.aboutlibraries:library:4.5.0'
    compile 'it.neokree:MaterialTabs:0.11'
    compile 'me.zhanghai.android.materialprogressbar:library:1.0.2'
}

/*retrolambda {
    jdk System.getenv("JAVA_HOME")
    oldJdk System.getenv("JAVA7_HOME")
    javaVersion JavaVersion.VERSION_1_7
    //jvmArgs '-noverify'
}*/

if(System.getenv("TRAVIS") && System.getenv("TRAVIS_TAG")) {
    play {
        serviceAccountEmail = System.getenv("SERVICE_EMAIL")
        pk12File = rootProject.file('play.p12')
        if(System.getenv("TRAVIS_TAG") && System.getenv("TRAVIS_TAG").contains("beta")) {
            track = 'beta'
        } else if(System.getenv("TRAVIS_TAG") && System.getenv("TRAVIS_TAG").contains("alpha")) {
            track = 'alpha'
        } else {
            track = 'production'
        }
    }
}

task travisBuild(dependsOn: [':RoosterPGPlus:clean', ':RoosterPGPlus:assembleRelease'])
if(System.getenv("TRAVIS_TAG")) {
    tasks.travisBuild.dependsOn(':RoosterPGPlus:publishApkRelease')
}
assembleRelease.mustRunAfter clean
